plugins {
	id 'org.springframework.boot' version '2.1.4.RELEASE'
	id 'com.bmuschko.docker-remote-api' version '3.2.0'
	id 'java'
}

apply plugin: 'io.spring.dependency-management'

group = 'com.example.app'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
	mavenCentral()
}

dependencies {
	compile 'org.springframework.boot:spring-boot-starter-actuator'
	compile 'org.springframework.boot:spring-boot-starter-web'
	testCompile 'org.springframework.boot:spring-boot-starter-test'
}

task copyJarToDockerPath(type: Copy) {
	from "build/libs/${project.name}-${project.version}.jar"
	into 'build/docker'
	rename { String fileName -> fileName.replace("-${project.version}", "") }
}

task createDockerfile(type: com.bmuschko.gradle.docker.tasks.image.Dockerfile) {
	dependsOn copyJarToDockerPath

	destFile = project.file('build/docker/Dockerfile')
	from 'openjdk:8u151-jre-slim' 
	copyFile "${project.name}.jar", "/opt/${project.name}.jar"
	exposePort (8081)
	entryPoint 'java', "-Djava.awt.headless=true", "-Xms256m", "-Xmx256m", '-jar', "/opt/${project.name}.jar"
}

task buildImage(type: com.bmuschko.gradle.docker.tasks.image.DockerBuildImage) {
	dependsOn createDockerfile
	inputDir = file('build/docker')
	tag = "${project.name}:${project.version.toLowerCase()}"
}